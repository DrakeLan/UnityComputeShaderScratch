// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel CSMain

Texture2D<float4> source;
RWTexture2D<float4> output;
float radius;
float edgeWidth;
int lines;
float4 tintColor;
float tintStrength;
float time;

float random (float2 pt, float seed) {
    const float a = 12.9898;
    const float b = 78.233;
    const float c = 43758.543123;
    return frac(sin(dot(pt, float2(a, b)) + seed) * c );
}

float inCircle( float2 pt, float2 center, float radius, float edgeWidth ){
    float len = length(pt - center);
    return 1.0 - smoothstep(radius-edgeWidth, radius, len);
}

[numthreads(8, 8, 1)]
void CSMain(uint3 id : SV_DispatchThreadID)
{
    uint2 index = (uint2(id.x, id.y)/3) * 3;
    float noise = random((float2)id.xy, time);
    float3 srcouceColor = lerp(source[id.xy].rgb*2, source[index].rgb, noise);


    float3 grayScale = (srcouceColor.r + srcouceColor.g + srcouceColor.b) / 3.0;
    float3 tinted = grayScale * tintColor.rgb;
    float3 finalColor = lerp(srcouceColor, tinted, tintStrength);

    float uvy = (float)id.y/(float)source.Length.y;
    float scanline = saturate(smoothstep(0.1, 0.2, frac(uvy * lines + time * 3)) + 0.3);
    finalColor = lerp(source[id.xy].rgb * 0.5, finalColor, scanline);

    float2 center = float2(source.Length.x*0.5, source.Length.y*0.5);
    center.x -= radius * 0.7;
    float leftCircle = inCircle((float2)id.xy, center.xy, radius, edgeWidth);
    center.x += radius * 1.4;
    float rightCircle = inCircle((float2)id.xy, center.xy, radius, edgeWidth);
    float inVision = saturate(leftCircle + rightCircle);
    finalColor = lerp(float3(0., 0., 0.), finalColor, inVision);

           
    output[id.xy] = float4(finalColor, 1);
}